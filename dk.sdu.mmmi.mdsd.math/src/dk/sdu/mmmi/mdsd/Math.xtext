grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Model:
	'program' name=ID
	externalDefinitions+=ExternalDefinition*
	variables+=GlobalVariable*;

GlobalVariable returns Variable:
	{GlobalVariable} 'var' name=ID '=' expression=AdditionAndSubtractionExpression;

LocalVariable returns Variable:
	{LocalVariable} 'let' name=ID '=' local_expression=AdditionAndSubtractionExpression 'in'
	expression=AdditionAndSubtractionExpression 'end';

AdditionAndSubtractionExpression returns Expression:
	MultiplicationAndDivisionExpression (({Plus.left=current} '+' | {Minus.left=current} '-')
	right=MultiplicationAndDivisionExpression)*;

MultiplicationAndDivisionExpression returns Expression:
	ValueExpression (({Multiplication.left=current} '*' | {Division.left=current} '/') right=ValueExpression)*;

ValueExpression returns Expression:
	{Parenthesis} '(' parenthesizedExpression=AdditionAndSubtractionExpression ')' | {Number} value=INT | LocalVariable
	| VariableReference | External;

VariableReference:
	variable=[Variable];

ExternalDefinition:
	PiExternalDefinition | SqrtExternalDefinition | PowExternalDefinition;

PiExternalDefinition returns ExternalDefinition:
	'external' {PiExternalDefinition} name='pi' '()'
;

SqrtExternalDefinition returns ExternalDefinition:
	'external' {SqrtExternalDefinition} name='sqrt' '(' type='int' ')'
;

PowExternalDefinition returns ExternalDefinition:
	'external' {PowExternalDefinition} name='pow' '(' type1='int' ',' type2='int' ')'
;

External:
	PiExternal | SqrtExternal | PowExternal;

PiExternal returns External:
	{PiExternal} name='pi' '()';

SqrtExternal returns External:
	{SqrtExternal} name='sqrt' '(' param1=AdditionAndSubtractionExpression ')';

PowExternal returns External:
	{PowExternal} name='pow' '(' param1=AdditionAndSubtractionExpression ',' param2=AdditionAndSubtractionExpression ')';
	
